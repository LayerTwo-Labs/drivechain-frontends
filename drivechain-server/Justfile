bdk-cli-features := "--no-default-features --features=key-value-db,compiler,electrum"
bdk-cli-git := "https://github.com/bitcoindevkit/bdk-cli.git"
bdk-cli-version := "v0.27.1"

build-go:
    go build -o drivechain-server .

build: build-bdk-cli build-go

clean:
    rm -rf ./bdk/bin
    go clean

lint:
    golangci-lint run ./...

gen:
    buf generate

install-bdk-cli:
    cargo install \
        --git {{bdk-cli-git}} \
        --tag {{bdk-cli-version}} \
        {{bdk-cli-features}}

build-bdk-cli:
    #!/usr/bin/env bash
    set -euo pipefail
    if [ ! -f "./bdk/bin/bdk-cli" ]; then
        echo "Building bdk-cli..."
        mkdir -p ./bin
        if ! command -v rustc &> /dev/null; then
            echo "Error: Rust is not installed. Please install Rust and try again."
            exit 1
        fi
        cargo install \
        --git {{bdk-cli-git}} \
        --tag {{bdk-cli-version}} \
        --root ./bdk \
        {{bdk-cli-features}}
        echo "bdk-cli has been built to ./bin/bdk-cli."
    else
        echo "bdk-cli already exists, skipping build"
    fi
